#######################################################
#
# Test parserealarray(), introduce 777 duplicate key
#
# The 5xx tests look at the same thing as their corresponding 4xx tests
# 500-519 are readrealarray, 520-539 test the same things for parseintarray
#
#######################################################

body common control
{
      inputs => { "../../default.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };   
      version => "1.0";
}

bundle agent init
{
  files:
      "$(G.testfile)"
      delete => init_delete;
      
      "$(G.testfile)"
      create => "true",
      edit_line => init_fill_in;
}

bundle edit_line init_fill_in
{
  insert_lines:

      "0:1:2
999.9:888:777:666.6
999.9:000
1:2:3";

}

body delete init_delete
{
      dirlinks => "delete";
      rmdirs   => "true";
}

#######################################################

bundle agent test
{
  vars:
      "teststr" string => readfile("$(G.testfile)",1000);
      "cnt" int => parserealarray("ary", "$(teststr)","NoComment",":",10,1000);
      "num" int => "3"; 	# 4 lines, but 3 unique keys
}

#######################################################

bundle agent check
{
  vars:
      "idx" slist => getindices("test.ary");

  classes:
      "good" and => {   
		      strcmp("$(test.num)", "$(test.cnt)"),

		      strcmp("$(test.ary[0][0])", "0"),
		      strcmp("$(test.ary[0][1])", "1"),
		      strcmp("$(test.ary[0][2])", "2"),

		      # The second value should overwrite the first
		      strcmp("$(test.ary[999.9][0])", "999.9"),
		      strcmp("$(test.ary[999.9][1])", "0"),

		      strcmp("$(test.ary[1][0])", "1"),
		      strcmp("$(test.ary[1][1])", "2"),
		      strcmp("$(test.ary[1][2])", "3"),
      };

      # Make sure there are no trailing elements
      "bad" or => {
		    isvariable("test.ary[0][3]"),
		    isvariable("test.ary[999.9][2]"),
		    isvariable("test.ary[1][3]"),
      };

      "ok" expression => "good&!bad";

  reports:
    DEBUG::
      "expected $(test.num) entries, saw $(test.cnt)";

      "saw array indices '$(idx)'";

      "expected test.ary[0][0] = '0', saw '$(test.ary[0][0])'";
      "expected test.ary[0][1] = '1', saw '$(test.ary[0][1])'";
      "expected test.ary[0][2] = '2', saw '$(test.ary[0][2])'";

      "expected test.ary[999.9][0] = '999.9', saw '$(test.ary[999.9][0])'";
      "expected test.ary[999.9][1] = '0', saw '$(test.ary[999.9][1])'";

      "expected test.ary[1][0] = '1', saw '$(test.ary[1][0])'";
      "expected test.ary[1][1] = '2', saw '$(test.ary[1][1])'";
      "expected test.ary[1][2] = '3', saw '$(test.ary[1][2])'";

    ok::
      "$(this.promise_filename) Pass";

    !ok::
      "$(this.promise_filename) FAIL";
}
