#######################################################
#
# Create an relative link to a file, expect absolute link promise to want
# to change it to an absolute link (but fail to)
#
#######################################################

body common control
{
  inputs => { "../../default.cf" };
  bundlesequence  => { "g", default("$(this.promise_filename)") };   
  version => "1.0";
nova_edition::
  host_licenses_paid => "5";
}

bundle common g
{
vars:
	"testfile" string => "/tmp/TEST.cfengine";

	# This extracts the inode number of the linked file
	"command" string => 'printf "%d", (stat("$(g.testfile)"))[1]';
	# This extracts the inode number of the symlink
	"Lcommand" string => 'printf "%d", (lstat("$(g.testfile)"))[1]';

	"source" string => "/etc/group";
	"source_command" string => 'printf "%d", (stat("$(g.source)"))[1]';
	"source_ino" string => execresult(
	    "/usr/bin/perl -le '$(g.source_command)'",
	    "noshell");

	"target" string => "/etc/group";
}

#######################################################

bundle agent init
{
files:
	"$(g.testfile)"
	    create => "true",
	    move_obstructions => "true",
	    link_from => init_link;
}

body link_from init_link
{
	source => "$(g.target)";
	link_type => "relative";
}

#######################################################

bundle agent test
{
files:
	"$(g.testfile)"
	    classes => init_if_failed("test_ok"),
	    link_from => test_link;
}

body link_from test_link
{
	source => "$(g.target)";
	link_type => "absolute";
}

body classes init_if_failed(c)
{
	repair_failed => { "$(c)" };
}

#######################################################

bundle agent check
{
vars:
	"result" string => execresult(
	    "/usr/bin/perl -le '$(g.command)'",
	    "noshell");
	"Lresult" string => execresult(
	    "/usr/bin/perl -le '$(g.Lcommand)'",
	    "noshell");

	# This tells us where the link points
	"Tcommand" string => 'print readlink "$(g.testfile)"';
	"link_target" string => execresult(
	    "/usr/bin/perl -le '$(Tcommand)'",
	    "noshell");

classes:
	"okL" not => strcmp("$(g.source_ino)", "$(Lresult)");
	"okT" not => strcmp("$(link_target)", "$(g.target)");
	"ok" and => { "test_ok", "okL", "okT",
			strcmp("$(g.source_ino)", "$(result)"),
			regcmp("\..*", "$(link_target)")
		    };

reports:
    DEBUG::
	"expected: '$(g.source_ino)'";
	"got:      '$(g.testfile)' => '$(link_target)'";
    ok::
	"$(this.promise_filename) Pass";
    MAIN.ok::
	"$(this.promise_filename) Pass"
		report_to_file => "$(G.logfile)";
    !ok::
	"$(this.promise_filename) FAIL";
    MAIN.!ok::
	"$(this.promise_filename) FAIL"
		report_to_file => "$(G.logfile)";
}

#######################################################

bundle agent fini
{
methods:
	"any" usebundle => default_fini("$(g.testfile)");
}
