#######################################################
#
# Delete a number of lines via an slist, different way, different patterns (different order than original)
# Two regions - Only the first region is selected
# select_region, doesn't include header or trailer
#
#######################################################

body common control
{
      inputs => { "../../default.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };     
      version => "1.0";
}

bundle agent init
{
  vars:
      "states" slist => { "actual", "expected" };

      "actual" string =>
      "header
header
BEGIN
    One potato
    Two potato
    Three potatoe
    Four
END
middle
middle
BEGIN
    One potato
    Two potato
    Three potatoe
    Four
END
trailer
trailer";
      
      "expected" string =>
      "header
header
BEGIN
    One potato
    Two potato
    Four
END
middle
middle
BEGIN
    One potato
    Two potato
    Three potatoe
    Four
END
trailer
trailer";
      
  files:
      "$(G.testfile).$(states)"
      create => "true",
      edit_line => init_insert("$(init.$(states))"),
      edit_defaults => init_empty;
}

bundle edit_line init_insert(str)
{
  insert_lines:
      "$(str)";
}

body edit_defaults init_empty
{
      empty_file_before_editing => "true";
}

#######################################################

bundle agent test
{
  files:
      "$(G.testfile).actual"
      edit_line => test_delete;

}

bundle edit_line test_delete
{
  vars:
      "str" slist => { ".*Three.*", ".*BEGIN.*", ".*END.*" };
      
  delete_lines:
      "$(str)"
      select_region => test_select;
}

body select_region test_select
{
      select_start => "BEGIN";
      select_end => "END";
}


#######################################################

bundle agent check
{
  methods:
      "any" usebundle => dcs_check_diff("$(G.testfile).actual",
					    "$(G.testfile).expected",
					    "$(this.promise_filename)");
}
### PROJECT_ID: core
### CATEGORY_ID: 27
