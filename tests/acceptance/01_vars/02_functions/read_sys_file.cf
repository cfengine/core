# Redmine#1032: Test that /sys and /proc files with 0 length are succesfully read by readfile() function

body common control
{
      inputs => { "../../default.cf.sub" };
      bundlesequence => { default("$(this.promise_filename)") };
      version => "1.0";
}

bundle agent init
{
}

bundle agent test
{
  classes:
      "have_sys" expression => fileexists("/sys/kernel/vmcoreinfo");
      "have_proc" expression => fileexists("/proc/meminfo");

  vars:
    have_sys::
      "sys_info" string => readfile("/sys/kernel/vmcoreinfo", 10000);
    !have_sys::
      "sys_info" string => "";

    have_proc::
      "proc_info" string => readfile("/proc/meminfo", 10000);
    !have_proc::
      "proc_info" string => "";
}

bundle agent check
{
  classes:
      "ok_proc" expression => regcmp(".*\d+.*", $(test.proc_info));
      "not_ok_proc" not => regcmp(".*\d+.*", $(test.proc_info));
      "ok_sys" expression => regcmp(".*\d+.*", $(test.sys_info));
      "not_ok_sys" not => regcmp(".*\d+.*", $(test.sys_info));

      "ok" not => classmatch("not_ok_.+");

  reports:
    DEBUG::
      "/proc test OK" ifvarclass => "ok_proc";
      "/sys test OK" ifvarclass => "ok_sys";
      "/proc test NOT OK: data '$(test.proc_info)' did not contain digits" ifvarclass => "not_ok_proc";
      "/sys test NOT OK: data '$(test.sys_info)' did not contain digits" ifvarclass => "not_ok_sys";
    DEBUG.inform_mode::
      "/proc test got data '$(test.proc_info)'";
      "/sys test got data '$(test.sys_info)'";
    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
}
