#  Copyright 2018 Northern.tech AS

#  This file is part of Cfengine 3 - written and maintained by Northern.tech AS.

#  This program is free software; you can redistribute it and/or modify it
#  under the terms of the GNU General Public License as published by the
#  Free Software Foundation; version 3.

#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA

# To the extent this program is licensed as part of the Enterprise
# versions of Cfengine, the applicable Commercial Open Source License
# (COSL) may apply to this file if you as a licensee so wish it. See
# included file COSL.txt.

#+begin_src prep
#@ ```
#@ rm -rf /tmp/testhere
#@ mkdir -p /tmp/testhere
#@ touch /tmp/testhere/a
#@ touch /tmp/testhere/b
#@ touch /tmp/testhere/c
#@ touch /tmp/testhere/d
#@ touch /tmp/testhere/e
#@ echo "#!/usr/bin/env sh" >/tmp/testhere/echo-stdout-and-stderr
#@ echo "echo stderr >&2" >>/tmp/testhere/echo-stdout-and-stderr
#@ echo "echo stdout" >>/tmp/testhere/echo-stdout-and-stderr
#@ chmod +x /tmp/testhere/echo-stdout-and-stderr
#@ ```
#+end_src
###############################################################################
#+begin_src cfengine3
body common control
{
      bundlesequence  => { "example" };
}

bundle agent example
{
  vars:
      "my_result"
        string => execresult("/bin/ls /tmp/testhere", noshell);

      "my_result_with_stdout_and_stderr"
        string => execresult("/tmp/testhere/echo-stdout-and-stderr", noshell);

      "my_result_with_stdout"
        string => execresult("/tmp/testhere/echo-stdout-and-stderr 2>/dev/null", useshell);

      "my_result_with_stderr"
        string => execresult("/tmp/testhere/echo-stdout-and-stderr 1>/dev/null", useshell);

  reports:
      "/bin/ls /tmp/testhere returned '$(my_result)'";
      "my_result_with_stdout_and_stderr == '$(my_result_with_stdout_and_stderr)'";
      "my_result_with_stdout == '$(my_result_with_stdout)'";
      "my_result_with_stderr == '$(my_result_with_stderr)'";

}
#+end_src
###############################################################################
#+begin_src example_output
#@ ```
#@ R: /bin/ls /tmp/testhere returned 'a
#@ b
#@ c
#@ d
#@ e
#@ echo-stdout-and-stderr'
#@ R: my_result_with_stdout_and_stderr == 'stderr
#@ stdout'
#@ R: my_result_with_stdout == 'stdout'
#@ R: my_result_with_stderr == 'stderr'
#@ ```
#+end_src
