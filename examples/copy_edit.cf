#  Copyright 2017 Northern.tech AS

#  This file is part of Cfengine 3 - written and maintained by Northern.tech AS.

#  This program is free software; you can redistribute it and/or modify it
#  under the terms of the GNU General Public License as published by the
#  Free Software Foundation; version 3.

#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA

# To the extent this program is licensed as part of the Enterprise
# versions of Cfengine, the applicable Commercial Open Source License
# (COSL) may apply to this file if you as a licensee so wish it. See
# included file COSL.txt.

########################################################
#
# Copy and edit convergently
#
########################################################

body common control

{
      bundlesequence  => { "example" };
      version => "1.2.3";
}

########################################################

bundle agent example

{
  vars:

      "source" string => "/tmp";
      "dest"   string => "/tmp";

  files:

      "/$(dest)/staging-file" 

      comment  => "Copy from source to buffer",
      copy_from    => cp("$(source)/source-template"),
      classes      => satisfied("copy_ok");

    copy_ok::

      "/$(dest)/final-file" 

      comment => "Build a file template and expand keys",
      edit_line => myedits("/$(dest)/staging-file"),
      edit_defaults => empty;

}

#########################################################

body copy_from cp(from)

{
      source      => "$(from)";
      compare     => "mtime";
      type_check  => "true";
}

########################################################

bundle edit_line myedits(f)

{
  insert_lines:

      "$(f)"

      comment     => "Populate empty file",
      insert_type => "file";

  replace_patterns:

      "TEMPLATE_HOST_KEY"

      comment => "Replace a place-marker with the name of this host",
      replace_with => rp("$(sys.host)");

}

########################################################

body replace_with rp(x)

{
      replace_value => "$(x)";
      occurrences => "all";
}

#########################################################

body classes satisfied(x)
{
      promise_repaired => { "$(x)" };
      persist_time => "0";
}

#######################################################

body edit_defaults empty

{
      empty_file_before_editing => "true";
}
