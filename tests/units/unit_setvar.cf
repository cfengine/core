
body common control
{
bundlesequence => { "setvars" };
}

bundle agent setvars
{
vars:

  # want to set these values by the names of their array keys

  "rhs[lhs1]" string => " Mary had a little pig";
  "rhs[lhs2]" string => "Whose Fleece was white as snow";
  "rhs[lhs3]" string => "And everywhere that Mary went";

  "rhs[net/ipv4/tcp_syncookies]" string => "1";
  "rhs[net/ipv4/icmp_echo_ignore_broadcasts]" string => "1";
  "rhs[net/ipv4/ip_forward]" string => "0";

  # oops, now change pig -> lamb

files:


  "/tmp/system"

        create => "true",
     edit_line => set_variable_values("setvars.rhs");

}

############################################################
# Library stuff
############################################################

bundle edit_line set_variable_values(v)

{
vars:

  "index" slist => getindices("$(v)");

  # Be careful if the index string contains funny chars

  "cindex[$(index)]" string => canonify("$(index)");

field_edits:

  # match a line starting like the key = something

  "$(index)\s*=.*"

     edit_field => col("=","2","$($(v)[$(index)])","set"),
        classes => if_ok("not_$(cindex[$(index)])");

insert_lines:

  "$(index)=$($(v)[$(index)])",

      ifvarclass => "!not_$(cindex[$(index)])";
}

############################################################

body edit_field col(split,col,newval,method)

{
field_separator    => "$(split)";
select_field       => "$(col)";
value_separator    => ",";
field_value        => "$(newval)";
field_operation    => "$(method)";
extend_fields      => "true";
allow_blank_fields => "true";
}


#

body classes if_ok(x)
{
promise_repaired => { "$(x)" };
promise_kept => { "$(x)" };
}
