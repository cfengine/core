##############################################################################
#
# Test that immutable file CANNOT be edited by agent without specifying
# fsattrs immutable constraint.
#
##############################################################################

body common control
{
  inputs => { "../../default.cf.sub" };
  bundlesequence  => { default("$(this.promise_filename)") };
  version => "1.0";
}

body fsattrs set_immutable
{
  immutable => "true";
}

body fsattrs set_mutable
{
  immutable => "false";
}

bundle agent init
{
  files:
    "/tmp/00_immutable.txt"
      content => "I'm immutable",
      fsattrs => set_immutable;
}

bundle agent test
{
  meta:
    "description" -> { "CFE-1840", "ENT-10961" }
      string => "Test that immutable file cannot be edited by agent without specifying fsattrs immutable constraint";

    "test_soft_fail"
      string => "hpux|aix|windows",
      meta => { "CFE-1840", "ENT-10961" };

  commands:
    "$(sys.cf_agent) -Kf $(this.promise_filename).sub";
}

bundle agent check
{
  vars:
    "expected"
      string => "I'm immutable";
    "actual"
      string => readfile("/tmp/00_immutable.txt");

  classes:
    "ok"
      expression => strcmp("$(actual)", "$(expected)");

  methods:
    "any"
      usebundle => dcs_passif("ok", "$(this.promise_filename)"),
      inherit => "true";

  reports:
      "Expected: '$(expected)', actual: '$(actual)'";
}

bundle agent destroy
{
  files:
    # Make sure immutable bit is not set
    "/tmp/00_immutable.txt"
      fsattrs => set_mutable,
      delete => tidy;
}
