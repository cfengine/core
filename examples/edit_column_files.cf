#  Copyright 2017 Northern.tech AS

#  This file is part of Cfengine 3 - written and maintained by Cfengine AS.

#  This program is free software; you can redistribute it and/or modify it
#  under the terms of the GNU General Public License as published by the
#  Free Software Foundation; version 3.

#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA

# To the extent this program is licensed as part of the Enterprise
# versions of Cfengine, the applicable Commercial Open Source License
# (COSL) may apply to this file if you as a licensee so wish it. See
# included file COSL.txt.

######################################################################
#
# File editing
#
# Normal ordering:
# - delete
# - replace | column_edit
# - insert
# 
######################################################################


body common control

{
      version => "1.2.3";
      bundlesequence  => { "bun"  };
}

########################################################

bundle agent bun

{
  vars:

      "userset" slist => { "one", "two", "three" };

  files:

      # Make a copy of the password file

      "/home/mark/tmp/passwd"

      create    => "true",
      edit_line => set_user_field("mark","6","/home/dir");

      "/home/mark/tmp/group"

      create    => "true",
      edit_line => append_user_field("wheel","4","@(bun.userset)");

}

########################################################

bundle edit_line set_user_field(user,field,val)
{
  field_edits:

      "$(user).*"

      # Set field of the file to parameter

      edit_field => col(":","$(field)","$(val)","set");
}

########################################################

bundle edit_line append_user_field(user,field,allusers)
{
  vars:

      "val" slist => { @(allusers) };

  field_edits:

      "$(user).*"

      # Set field of the file to parameter

      edit_field => col(":","$(field)","$(val)","alphanum");

}

########################################
# Bodies
########################################

body edit_field col(split,col,newval,method)

{
      field_separator    => "$(split)";
      select_field       => "$(col)";
      value_separator    => ",";
      field_value        => "$(newval)";
      field_operation    => "$(method)";
      extend_fields      => "true";
      allow_blank_fields => "true";
}

