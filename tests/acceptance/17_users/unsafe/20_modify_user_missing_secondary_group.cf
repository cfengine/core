body common control
{
      inputs => { "../../default.cf.sub", "user_queries.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };
}

bundle agent init
{
  commands:
    "/usr/bin/sudo $(user_tests.pw_path) userdel testu";
    "/usr/bin/sudo $(user_tests.pw_path) delgroup testu";
    "/usr/bin/sudo $(user_tests.pw_path) delgroup testg1";
    "/usr/bin/sudo $(user_tests.pw_path) delgroup testg2";
    "/usr/bin/sudo $(user_tests.pw_path) groupadd testg1";
    "/usr/bin/sudo $(user_tests.pw_path) useradd testu";
    freebsd::
      "/usr/bin/sudo $(user_tests.pw_path) usermod testu --groups testg1 --append";
    !freebsd::
      "/usr/bin/sudo $(user_tests.pw_path) usermod --groups testg1 --append testu";
}

bundle agent test
{
  meta:
    "description" -> { "ENT-3710" }
      string => "Managing a user without specifying any secondary groups attribute
                 should result in no changes to existing secondary groups.";

  users:
    "testu"
      policy => "present";
}

bundle agent check
{
  methods:
    "any" usebundle => user_is_in_secondary_group("testu", "testg1", "testg1_success", "testg1_failure"),
      classes => always("testg1_method_run");

    "any" usebundle => user_is_in_secondary_group("testu", "testg2", "testg2_success", "testg2_failure"),
      classes => always("testg2_method_run");

    "any" usebundle => user_exists("testu", "testu_success", "testu_failure"),
      classes => always("testu_method_run");

  classes:
    "ready" and => { "testg1_method_run", "testg2_method_run", "testu_method_run" };
    "ok" and => { "testg1_success", "!testg1_failure",
                  "!testg2_success", "testg2_failure",
                  "testu_success", "!testu_failure" };

  reports:
    ok.ready::
      "$(this.promise_filename) Pass";
    !ok.ready::
      "$(this.promise_filename) FAIL";
}
