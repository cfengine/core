#######################################################
#
# Test cf-serverd related promises
#
# Tests copy_from digest verify (MD5)
#
#######################################################

body common control
{
      inputs => { "../../default.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };
      version => "1.0";
}

#######################################################

bundle agent init
{
}

bundle agent test
{
  files:
      "$(G.testdir)/destination_file"
      copy_from => copy_src_file,
      classes => if_repaired("copy_ok");
}

#########################################################

body copy_from copy_src_file
{
      source      => "$(G.testdir)/source_file";
      servers     => { "127.0.0.1" };
      copy_backup => "false";

      portnumber => "9876"; # localhost_open

      encrypt     => "false";
      compare     => "digest";
      verify      => "true";

      #purge       => "false";
      #type_check  => "true";
      #force_ipv4  => "true";
      trustkey => "true";
}

#######################################################

bundle agent check
{
  methods:
    copy_ok::
      "any" usebundle => dcs_check_diff("$(G.testdir)/source_file","$(G.testdir)/destination_file","$(fn[1])");

  classes:
      "dummy" expression => regextract("(.*)\.sub", $(this.promise_filename), "fn");

  reports:
    !copy_ok::
      "$(fn[1]) FAIL";
}
