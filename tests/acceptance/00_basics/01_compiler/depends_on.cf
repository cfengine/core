# Test depends_on

body common control
{
      inputs => { "../../default.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };
      version => "1.0";
}

bundle agent test
{
  vars:
      "expandit" slist => { "hc2" };

  methods:
      "zebra" usebundle => zebra:zebra;

  classes:
      "c1" expression => "any",
      scope => "namespace",
      handle => "hc1",
      depends_on => { };

      "c2" expression => "any",
      scope => "namespace",
      handle => "hc2",
      depends_on => { "hc1", "me2" };

      "c3" expression => "any",
      scope => "namespace",
      handle => "hc3",
      depends_on => { "$(expandit)" };

      "c4" expression => "any",
      scope => "namespace",
      handle => "hc4",
      depends_on => { "nosuchhandle" };

      "c5" expression => "any",
      scope => "namespace",
      handle => "hc5",
      depends_on => { "zebra.zebra" };

      "c6" expression => "any",
      scope => "namespace",
      handle => "hc6",
      depends_on => { "zebra:zebra" };

  reports:
      "me too" handle => "me2";
}

bundle agent check
{
  methods:
      "" usebundle => dcs_passif_expected("c1,c2,c3", # expected
                                          "c4,c5,c6", # not expected
                                          $(this.promise_filename)),
      inherit => "true";
}

body file control
{
      namespace => "zebra";
}

bundle agent zebra
{
  classes:
      "zebra" expression => "any",
      scope => "namespace",
      handle => "zebra";
}
