#######################################################
#
# Test findfiles()
#
#######################################################

body common control
{
      inputs => { "../../default.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };
      version => "1.0";
}

bundle common findfiles
{
  vars:
    # * in filenames not allowed on win
    windows::
      "names" slist => { "a", "bc", "d/e/f", "g/h/i/j", "klm/nop/qrs" };
    !windows::
      "names" slist => { "a", "bc", "d/e/f", "g/h/i/j", "klm/nop/qrs", "tu/*" };
}

#######################################################

bundle agent init
{
  files:
      "$(G.testdir)/$(findfiles.names)"
      create => "true";

  reports:
    DEBUG::
      "Created $(G.testdir)/$(findfiles.names)";
}


#######################################################

bundle agent test
{
  meta:
      "test_suppress_fail" string => "windows",
        meta => { "redmine4730" };

  vars:
      "patterns[a]" string => "$(G.testdir)/?";
      "patterns[b]" string => "$(G.testdir)/*";
      "patterns[c]" string => "$(G.testdir)/?/*";
      "patterns[d]" string => "$(G.testdir)/[ab]*";
      "patterns[e]" string => "$(G.testdir)/nosuch/*";
      "patterns[relative_path_1]" string => "./*";
      "patterns[relative_path_2]" string => "**";
      "patterns[relative_path_3]" string => "../**";

    !windows::
      "patterns[f]" string => "$(G.testdir)/tu/\\*";
      "patterns[g]" string => "$(G.testdir)/**";
      "patterns[h]" string => "$(G.testdir)/**/j";

    any::
      "pnames" slist => getindices("patterns");

      "found[$(pnames)]" slist => findfiles("$(patterns[$(pnames)])");
      "found_string[$(pnames)]" string => join(",", "found[$(pnames)]");


  reports:
    DEBUG::
      "findfiles pattern $(pnames) '$(patterns[$(pnames)])' => '$(found_string[$(pnames)])'";
}


#######################################################

bundle agent check
{
  vars:
      "expected[a]" string => "$(G.testdir)/a,$(G.testdir)/d,$(G.testdir)/g";
    windows::
      "expected[b]" string => "$(G.testdir)/a,$(G.testdir)/bc,$(G.testdir)/d,$(G.testdir)/g,$(G.testdir)/klm";
    !windows::
      "expected[b]" string => "$(G.testdir)/a,$(G.testdir)/bc,$(G.testdir)/d,$(G.testdir)/g,$(G.testdir)/klm,$(G.testdir)/tu";

    any::
      "expected[c]" string => "$(G.testdir)/d/e,$(G.testdir)/g/h";
      "expected[d]" string => "$(G.testdir)/a,$(G.testdir)/bc";
      "expected[e]" string => "";

    !windows::
      "expected[f]" string => "$(G.testdir)/tu/*";
      "expected[g]" string => "$(G.testdir)/a,$(G.testdir)/bc,$(G.testdir)/d,$(G.testdir)/g,$(G.testdir)/klm,$(G.testdir)/tu,$(G.testdir)/d/e,$(G.testdir)/g/h,$(G.testdir)/klm/nop,$(G.testdir)/tu/*,$(G.testdir)/d/e/f,$(G.testdir)/g/h/i,$(G.testdir)/klm/nop/qrs,$(G.testdir)/g/h/i/j";
      "expected[h]" string => "$(G.testdir)/g/h/i/j";
      # relative paths are skipped, thus return empty list
      "expected[relative_path_1]" string => "";
      "expected[relative_path_2]" string => "";
      "expected[relative_path_3]" string => "";

    any::
      "expects" slist => getindices("expected");

      "fstring" slist => getindices("test.found_string");

      "joint_condition" string => join(".", "expects");

  classes:
      "$(expects)" expression => strcmp("$(test.found_string[$(expects)])", "$(expected[$(expects)])");
      "ok" expression => "$(joint_condition)";

  reports:
    DEBUG::
      "pattern $(expects) matches as expected: '$(expected[$(expects)])'"
      ifvarclass => "$(expects)";

      "pattern $(expects) does NOT match expected: '$(test.found_string[$(expects)])' != '$(expected[$(expects)])'"
      ifvarclass => "!$(expects)";

    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
}
