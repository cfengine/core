#######################################################
#
# Test that action => "warn" works correctly for rename { rotate => 0 } (Issue 841)
#
#######################################################

body common control
{
  inputs => { "../../default.cf.sub" };
  bundlesequence  => { default("$(this.promise_filename)") };
  version => "1.0";
}


#######################################################

bundle agent init
{
vars:
  "states" slist => { "actual", "expected" };

  "actual" string => "someline";
  "expected" string => "someline";

files:
  "$(G.testfile).$(states)"
    create => "true",
    edit_line => init_insert("$(init.$(states))"),
    edit_defaults => init_empty;
}

bundle edit_line init_insert(str)
{
insert_lines:
  "$(str)";
}

body edit_defaults init_empty
{
  empty_file_before_editing => "true";
}

#######################################################

bundle agent test
{
files:
  "$(G.testfile).actual"
    rename => rotate,
    action => warn_only;
}

body rename rotate
{
rotate => "0";
}

body action warn_only
{
action_policy => "warn";
ifelapsed => "60";
}

#######################################################

bundle agent check
{
methods:
  "any" usebundle => default_check_diff("$(G.testfile).actual",
                                        "$(G.testfile).expected",
                                        "$(this.promise_filename)");
}

