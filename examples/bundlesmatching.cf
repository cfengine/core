#  Copyright 2017 Northern.tech AS

#  This file is part of Cfengine 3 - written and maintained by Northern.tech AS.

#  This program is free software; you can redistribute it and/or modify it
#  under the terms of the GNU General Public License as published by the
#  Free Software Foundation; version 3.

#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA

# To the extent this program is licensed as part of the Enterprise
# versions of Cfengine, the applicable Commercial Open Source License
# (COSL) may apply to this file if you as a licensee so wish it. See
# included file COSL.txt.

#+begin_src cfengine3
body common control
{
      bundlesequence => { mefirst };
}

bundle common g
{
  vars:

      # Here we find all bundles in the default namespace whos name starts with
      # run.

      "todo" slist => bundlesmatching("default:run.*");
}

bundle agent mefirst
{
  methods:

      # Here, we actuate each of the bundles that were found using
      # bundlesmatching in bundle common g.

      "" usebundle => $(g.todo);
}

bundle agent run_deprecated
{
  meta:
      # This bundle is tagged with deprecated
      "tags" slist => { "deprecated" };
}

bundle agent run_123_456
{
  vars:
      # Here we find all bundles in our policy.
      "bundles" slist => bundlesmatching(".*");

      # Here we find all the bundles that are tagged as deprecated.
      "deprecated_bundles" slist => bundlesmatching(".*", "deprecated");

      # Here we find all bundles that match 891 (none will).
      "no_bundles" slist => bundlesmatching("891");

  reports:
      # Here we report on our findings:
      "bundles = $(bundles)";
      "deprecated bundles = $(deprecated_bundles)";
      "no bundles = $(no_bundles)";
}
#+end_src
###############################################################################
#+begin_src example_output
#@ ```
#@ R: bundles = default:run_123_456
#@ R: bundles = default:run_deprecated
#@ R: bundles = default:mefirst
#@ R: bundles = default:g
#@ R: deprecated bundles = default:run_deprecated
#@ ```
#+end_src
