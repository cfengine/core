#######################################################
#
# Insert a number of lines at the end of a fully bounded
# region, after the first instance of a line
#
#######################################################

body common control
{
  inputs => { "../../default.cf" };
  bundlesequence  => { "g", default("$(this.promise_filename)") };   
  version => "1.0";
nova_edition::
  host_licenses_paid => "5";
}

bundle common g
{
vars:
	"testfile" string => "/tmp/TEST.cfengine";
}

#######################################################

bundle agent init
{
vars:
	"states" slist => { "actual", "expected" };

	"actual" string =>
"	big potatoes
BEGIN
    One potato
    Two potato
    Two potatos
    Four
END
	small potatoes";
 
	"expected" string =>
"	big potatoes
BEGIN
    One potato
    Three potatoe
    Two potato
    Two potatos
    Four
END
	small potatoes";
 
files:
	"$(g.testfile).$(states)"
	    create => "true",
	    edit_line => init_insert("$(init.$(states))"),
	    edit_defaults => init_empty;
}

bundle edit_line init_insert(str)
{
insert_lines:
	"$(str)";
}

body edit_defaults init_empty
{
	empty_file_before_editing => "true";
}

#######################################################

bundle agent test
{
vars:
      "tstr" string =>
"    One potato
    Two potato
    Three potatoe
    Four";
 
files:
	"$(g.testfile).actual"
	    create => "true",
	    edit_line => test_insert("$(test.tstr)");

}

bundle edit_line test_insert(str)
{
insert_lines:
	"$(str)"
	    select_region => test_region,
	    location => test_after_first(".*potato.*");
}

body select_region test_region
{
select_start => "BEGIN";
select_end => "END";
}

body location test_after_first(line)
{
before_after => "after";
first_last => "first";
select_line_matching => "$(line)";
}

#######################################################

bundle agent check
{
methods:
	"any" usebundle => default_check_diff("$(g.testfile).actual",
					      "$(g.testfile).expected",
					      "$(this.promise_filename)");
}

#######################################################

bundle agent fini
{
methods:
	"any" usebundle => default_fini("$(g.testfile)");
}
