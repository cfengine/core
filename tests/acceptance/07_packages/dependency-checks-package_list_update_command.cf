#######################################################
# Test for presence of "main" attribute, given the presence of "dependent"
# attributes, which do not make sense without the "main" attribute.
#
# main attribute: package_list_update_command
# dependent attributes: 
#     package_list_update_ifelapsed
#######################################################

body common control
{
  inputs => { "../default.cf.sub" };
  bundlesequence  => { default("$(this.promise_filename)") };
  version => "1.0";
nova_edition::
  host_licenses_paid => "5";
}

bundle agent init
{
vars:
  "dummy" string => "dummy";
}

bundle agent test
{
packages:
  "bash-1.0-amd64"
    package_policy => "add",
    package_method => mock_no_list_update_list_update_ifelapsed,
    classes => test_set_class("no_list_update_list_update_ifelapsed_succ",
                              "no_list_update_list_update_ifelapsed_fail");
}

body package_method mock_no_list_update_list_update_ifelapsed
{
  package_changes => "individual";

  package_name_regex => "^([^-]+)";
  package_version_regex => "^[^-]+-([^-]+)";
  package_arch_regex => "^[^-]+-[^-]+-(.*)";

  package_list_command => "/usr/bin/printf 'bash-1.0-amd64'";
  package_installed_regex => ".*";
  package_list_name_regex => "^([^-]+)";
  package_list_version_regex => "^[^-]+-([^-]+)";
  package_list_arch_regex => "^[^-]+-[^-]+-(.*)";

  package_add_command => "$(G.false)";
  package_update_command => "$(G.false)";
  package_delete_command => "$(G.false)";
  package_verify_command => "$(G.false)";
  
  package_list_update_ifelapsed => "240";
}

body classes test_set_class(ok_class,notok_class)
{
promise_kept => { "$(ok_class)" };
promise_repaired => { "$(ok_class)" };
repair_failed => { "$(notok_class)" };
}

#######################################################

bundle agent check
{
classes:
  "ok" and => { "!no_list_update_list_update_ifelapsed_succ", "no_list_update_list_update_ifelapsed_fail",
                };

reports:
  ok::
    "$(this.promise_filename) Pass";
  !ok::
    "$(this.promise_filename) FAIL";
}