#
#  Copyright (C) CFEngine AS
#
#  This file is part of CFEngine 3 - written and maintained by CFEngine AS.
#
#  This program is free software; you can redistribute it and/or modify it
#  under the terms of the GNU General Public License as published by the
#  Free Software Foundation; version 3.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA
#
# To the extent this program is licensed as part of the Enterprise
# versions of CFEngine, the applicable Commercial Open Source License
# (COSL) may apply to this file if you as a licensee so wish it. See
# included file COSL.txt.
#
################################################################################
## CFEngine Bootstrap / Failsafe Policy ########################################
################################################################################
# THIS FILE REPRESENTS A FALL-BACK SOLUTION FOR THE PRIMARY FAILSAFE FILE.
# IF THE PRIMARY FAILSAFE/UPDATE LOSES FUNCTIONALITY DUE TO MODIFICATIONS MADE
# BY THE USER, CFENGINE WILL RECOVER BY USING THIS FALL-BACK BOOTSTRAPPED FILE.
# NEVER EDIT THIS FILE, YOU WILL HAVE TO LOG ON TO EVERY NODE MANAGED BY
# CFENGINE TO RECTIFY POTENTIAL ERRORS IF SOMETHING GOES WRONG.
################################################################################

body common control
{
      bundlesequence => { "cfe_internal_update", "cfe_internal_call_update" };
}

################################################################################

body agent control
{
      skipidentify => "true";
}

################################################################################

bundle agent cfe_internal_update
{
  classes:

    any::

      "have_ppkeys"
      expression => fileexists("$(sys.workdir)/ppkeys/localhost.pub"),
      handle => "cfe_internal_bootstrap_update_classes_have_ppkeys";

      "have_promises_cf"
      expression => fileexists("$(sys.inputdir)/promises.cf"),
      handle => "cfe_internal_bootstrap_update_classes_have_promises_cf";

      #

  commands:

    !have_ppkeys::

      "$(sys.cf_key)"
      handle => "cfe_internal_bootstrap_update_commands_generate_keys";

      #

  files:


    have_ppkeys::

      "$(sys.inputdir)"
      handle => "cfe_internal_bootstrap_update_files_sys_workdir_inputs_shortcut",
      copy_from => u_scp("masterfiles"),
      depth_search => u_recurse("inf"),
      classes => repaired("got_policy");

    !windows.have_ppkeys.got_policy_failed::

      "$(sys.inputdir)"
      handle => "cfe_internal_bootstrap_update_files_sys_workdir_inputs_not_windows",
      copy_from => u_scp("$(sys.masterdir)"),
      depth_search => u_recurse("inf"),
      classes => repaired("got_policy");

    windows.have_ppkeys.got_policy_failed::

      "$(sys.inputdir)"
      handle => "cfe_internal_bootstrap_update_files_sys_workdir_inputs_windows",
      copy_from => u_scp("/var/cfengine/masterfiles"), # Not $(sys.masterdir) when referring to remote host.
      depth_search => u_recurse("inf"),
      classes => repaired("got_policy");

    windows.have_ppkeys::

      "$(sys.workdir)\\bin-twin\\."
      handle => "cfe_internal_bootstrap_update_files_sys_workdir_bin_twin_windows",
      comment => "Make sure we maintain a clone of the binaries and libraries for updating",
      copy_from => u_cp("$(sys.workdir)\\bin\\."),
      depth_search => u_recurse("1");

      #

  processes:

    !windows.got_policy::

      "cf-execd" restart_class => "start_exec",
      handle => "cfe_internal_bootstrap_update_processes_start_cf_execd";

    am_policy_hub.got_policy::

      "cf-serverd" restart_class => "start_server",
      handle => "cfe_internal_bootstrap_update_processes_start_cf_serverd";

      #

  commands:

    start_exec.!windows.!systemd::

      "$(sys.cf_execd)"
      handle => "cfe_internal_bootstrap_update_commands_check_sys_cf_execd_start",
      classes => repaired("executor_started");

    start_server.!systemd::

      "$(sys.cf_serverd)"
      handle => "cfe_internal_bootstrap_update_commands_check_sys_cf_serverd_start",
      action => ifwin_bg,
      classes => repaired("server_started");

    start_exec.systemd::

      # We explicitly use "restart", because it is possible that cf-serverd
      # is running, even if cf-execd isn't, for example. Here we want to be
      # sure we relaunch everything.

      "/usr/bin/systemctl restart cfengine3"
      handle => "cfe_internal_bootstrap_update_commands_systemd_cfe_start",
      classes => repaired("executor_started");

      #

  services:

    windows.got_policy::

      "CfengineNovaExec"
      handle => "cfe_internal_bootstrap_update_services_windows_executor",
      service_policy => "start",
      service_method => bootstart,
      classes => repaired("executor_started");

      #

  reports:

    bootstrap_mode.am_policy_hub::

      "This host assumes the role of policy server"
      handle => "cfe_internal_bootstrap_update_reports_assume_policy_hub";

    bootstrap_mode.!am_policy_hub::

      "This autonomous node assumes the role of voluntary client"
      handle => "cfe_internal_bootstrap_update_reports_assume_voluntary_client";

    got_policy::

      "Updated local policy from policy server"
      handle => "cfe_internal_bootstrap_update_reports_got_policy";

    !got_policy.!have_promises_cf.have_ppkeys::

      "Failed to copy policy from policy server at $(sys.policy_hub):$(sys.masterdir)
       Please check
       * cf-serverd is running on $(sys.policy_hub)
       * network connectivity to $(sys.policy_hub) on port 5308
       * masterfiles 'body server control' - in particular allowconnects, trustkeysfrom and skipverify
       * masterfiles 'bundle server' -> access: -> masterfiles -> admit/deny
       It is often useful to restart cf-serverd in verbose mode (cf-serverd -v) on $(sys.policy_hub) to diagnose connection issues.
       When updating masterfiles, wait (usually 5 minutes) for files to propagate to inputs on $(sys.policy_hub) before retrying."
      handle => "cfe_internal_bootstrap_update_reports_did_not_get_policy";

    server_started::

      "Started the server"
      handle => "cfe_internal_bootstrap_update_reports_started_serverd";

    am_policy_hub.!server_started.!have_promises_cf.have_ppkeys::

      "Failed to start the server"
      handle => "cfe_internal_bootstrap_update_reports_failed_to_start_serverd";

    executor_started::

      "Started the scheduler"
      handle => "cfe_internal_bootstrap_update_reports_started_execd";

    !executor_started.!have_promises_cf.have_ppkeys::

      "Did not start the scheduler"
      handle => "cfe_internal_bootstrap_update_reports_failed_to_start_execd";

    !executor_started.have_promises_cf::

      "You are running a hard-coded failsafe. Please use the following command instead.
        $(sys.cf_agent) -f $(sys.inputdir)/update.cf"
      handle => "cfe_internal_bootstrap_update_reports_run_another_failsafe_instead";
      
}

################################################################################
bundle agent cfe_internal_call_update
{
  commands:

    # On Windows we need cf-execd to call update.cf, otherwise the daemons will
    # not run under the SYSTEM account.
    !windows::

      "$(sys.cf_agent) -f update.cf"
      handle => "cfe_internal_call_update_commands_call_update_cf";

}
############################################
body classes repaired(x)
{
      promise_repaired => {"$(x)"};
      repair_failed => {"$(x)_failed"};
}
############################################
body perms u_p(p)
{
      mode  => "$(p)";
}
#############################################
body copy_from u_scp(from)
{
      source      => "$(from)";
      compare     => "digest";
      trustkey    => "true";
    !am_policy_hub::
      servers => { "$(sys.policy_hub)" };
}
############################################
body action u_background
{
      background => "true";
}
############################################
body depth_search u_recurse(d)
{
      depth => "$(d)";
      exclude_dirs => { "\.svn", "\.git" };
}
############################################
body service_method bootstart
{
      service_autostart_policy => "boot_time";
}
############################################
body action ifwin_bg
{
    windows::
      background => "true";
}
############################################
body copy_from u_cp(from)
{
      source          => "$(from)";
      compare         => "digest";
      copy_backup     => "false";
}
