#######################################################
#
# Test that body action_policy warn restricts mustache
# template rendering.
# Ref: https://dev.cfengine.com/issues/6739
#
#######################################################

body common control
{
      inputs => { "../default.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };
      version => "1.0";
}

#######################################################

bundle agent init
{
  vars:
    "template_target" string => "$(G.testfile)";

   files:
    "$(template_target)"
      delete => tidy;

    "$(template_target)"
      create => "true";
}

bundle agent test
{
  vars:
    "template_target" string => "$(init.template_target)";

  files:
    # The template should not be rendered because dryrun is set
    "$(template_target)"
      edit_template => "$(this.promise_filename).mustache",
      template_method => "mustache",
      action => warn_only;
}

#######################################################

bundle agent check
{
  meta:
    "test_soft_fail"
      string => "any",
      meta => { "redmine#6739" };

  classes:
    "fail" expression => regline("SHOULD NOT RENDER", $(test.template_target) );

  reports:
    !fail::
      "$(this.promise_filename) Pass";
    fail::
      "$(this.promise_filename) FAIL";
}
### PROJECT_ID: core
### CATEGORY_ID: 27
