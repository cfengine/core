#######################################################
#
# Delete a line using a list, some of which match
# Two regions - Only the first region is going to be selected
# select_region, doesn't include header or trailer
# The current result of this test contradicts our logic.
# On the first iteration one value is deleted.
# On the second iteration no values are deleted. The second
# iteration should be independent from the first one, therefore
# I'm not fixing this test until we get a definitive answer on
# that.
#
#######################################################

body common control
{
      inputs => { "../../default.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };   
      version => "1.0";
}

bundle agent init
{
  vars:
      "states" slist => { "actual", "expected" };

      "actual" string =>
      "header
header
BEGIN
    One potato
    Two potato
    Three potatoe
    Four
END
middle
middle
BEGIN
    One potato
    Two potato
    Three potatoe
    Four
END
trailer
trailer";
      
      "expected" string =>
      "header
header
BEGIN
    Two potato
    Three potatoe
    Four
END
middle
middle
BEGIN
    Two potato
    Three potatoe
    Four
END
trailer
trailer";
      
  files:
      "$(G.testfile).$(states)"
      create => "true",
      edit_line => init_insert("$(init.$(states))"),
      edit_defaults => init_empty;
}

bundle edit_line init_insert(str)
{
  insert_lines:
      "$(str)";
}

body edit_defaults init_empty
{
      empty_file_before_editing => "true";
}

#######################################################

bundle agent test
{
  vars:
      "tstr" slist => { ".*T.*", ".*C.*" };
      
  files:
      "$(G.testfile).actual"
      edit_line => test_delete(".*potat.*", "@(test.tstr)");

}

bundle edit_line test_delete(str, match)
{
  delete_lines:
      "$(str)"
      delete_select => test_match(@{match}),
      select_region => test_select;
}

body select_region test_select
{
      select_start => "BEGIN";
      select_end => "END";
}


body delete_select test_match(m)
{
      delete_if_not_match_from_list => { @{m} };
}

#######################################################

bundle agent check
{
  methods:
      "any" usebundle => default_check_diff("$(G.testfile).actual",
					    "$(G.testfile).expected",
					    "$(this.promise_filename)");
}
