#######################################################
#
# Redmine #4791: body perms mode
#
#######################################################

body common control
{
      inputs => { "../../default.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };
      version => "1.0";
}

#######################################################

bundle agent test
{
  vars:
    freebsd|solaris::
      "modes" slist => { "1", "333", "100", "200", "777", "o=x", "ugo=wx", "u=x", "u=w", "ugo=rwx" };
    !freebsd.!solaris::
      "modes" slist => { "1", "333", "100", "200", "777", "1111", "1010", "13400", "22222", "o=x", "ugo=wx", "u=x", "u=w", "ugo=rwx" };

  files:
      "$(G.testdir)/$(modes)"
      create => "true",
      perms => test_mode($(modes));
}

body perms test_mode(m)
{
      mode => "$(m)";
}

bundle agent check
{
  vars:
      "modes" slist => { @(test.modes) };
    windows::
      "win_r" string => "100444";
      "win_w" string => "100666";
      "expected" data => parsejson('
{
  "1": "$(win_r)",
  "333": "$(win_w)",
  "100": "$(win_r)",
  "200": "$(win_w)",
  "777": "$(win_w)",
  "1111": "$(win_r)",
  "1010": "$(win_r)",
  "13400": "$(win_r)",
  "22222": "$(win_r)",
  "o=x": "$(win_r)",
  "ugo=wx": "$(win_w)",
  "u=x": "$(win_r)",
  "u=w": "$(win_w)",
  "ugo=rwx": "$(win_w)",
}');

    freebsd|solaris::
      "expected" data => parsejson('
{
  "1": "100001",
  "333": "100333",
  "100": "100100",
  "200": "100200",
  "777": "100777",
  "o=x": "100001",
  "ugo=wx": "100333",
  "u=x": "100100",
  "u=w": "100200",
  "ugo=rwx": "100777",
}');

    !freebsd.!solaris.!windows::
      "expected" data => parsejson('
{
  "1": "100001",
  "333": "100333",
  "100": "100100",
  "200": "100200",
  "777": "100777",
  "1111": "101111",
  "1010": "101010",
  "13400": "103400",
  "22222": "102222",
  "o=x": "100001",
  "ugo=wx": "100333",
  "u=x": "100100",
  "u=w": "100200",
  "ugo=rwx": "100777",
}');

    any::
      "actual[$(modes)]" string => filestat("$(G.testdir)/$(modes)", "modeoct");
      "canonified[$(modes)]" string => canonify("$(modes)");

  classes:
      "ok_$(canonified[$(modes)])" expression => strcmp("$(expected[$(modes)])", "$(actual[$(modes)])");

      freebsd|solaris::
      "ok" and => { "ok_1", "ok_333", "ok_100", "ok_200", "ok_777", classify("ok_o=x"), classify("ok_ugo=wx"), classify("ok_u=x"), classify("ok_u=w"), classify("ok_ugo=rwx") };

      !freebsd.!solaris::
      "ok" and => { "ok_1", "ok_333", "ok_100", "ok_200", "ok_777", "ok_1111", "ok_1010", "ok_13400", "ok_22222", classify("ok_o=x"), classify("ok_ugo=wx"), classify("ok_u=x"), classify("ok_u=w"), classify("ok_ugo=rwx") };

  reports:
    DEBUG::
      "Permission $(modes) worked, got $(expected[$(modes)])" ifvarclass => "ok_$(canonified[$(modes)])";
      "Permission $(modes) failed, expected $(expected[$(modes)]) != actual $(actual[$(modes)])" ifvarclass => "!ok_$(canonified[$(modes)])";
    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
}

### PROJECT_ID: core
### CATEGORY_ID: 27
