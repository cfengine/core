##############################################################################
#
# Test that agent can override the immutable bit when using copy_from
# attribute
#
##############################################################################

body common control
{
  inputs => { "../../default.cf.sub" };
  bundlesequence  => { default("$(this.promise_filename)") };
  version => "1.0";
}

bundle agent global
{
  vars:
    "testfile"
      string => "/tmp/03_immutable.txt";
}

body fsattrs set_immutable
{
  immutable => "true";
}

bundle agent init
{
  files:
    "$(global.testfile)"
      content => "I'm immutable";
    "/tmp/content.txt"
      content => "But agent can override";

  commands:
    "chattr +i $(global.testfile)"
      contain => in_shell;
}

bundle agent test
{
  meta:
    "description" -> { "CFE-1840", "ENT-10961" }
      string => "Test that agent can override the immutable bit on a file while using the copy_from attribute";

    "test_skip_unsupported"
      string => "hpux|aix|solaris|windows";

  files:
    "$(global.testfile)"
      fsattrs => set_immutable,
      copy_from => local_dcp("/tmp/content.txt");
}

bundle agent check
{
  vars:
    "expected"
      string => "But agent can override";
    "actual"
      string => readfile("$(global.testfile)");

  classes:
    "ok"
      expression => strcmp("$(actual)", "$(expected)");

  reports:
    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
    any::
      "Expected: '$(expected)', actual: '$(actual)'";
}

bundle agent destroy
{
  commands:
    "chattr -i $(global.testfile)"
      contain => in_shell,
      handle => "is mutable";

  files:
    "$(global.testfile)"
      delete => tidy,
      depends_on => { "is mutable" };
    "$(global.testfile).cfsaved"
      delete => tidy;
    "/tmp/content.txt"
      delete => tidy;
}
