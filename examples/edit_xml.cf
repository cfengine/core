#  Copyright 2017 Northern.tech AS

#  This file is part of Cfengine 3 - written and maintained by Northern.tech AS.

#  This program is free software; you can redistribute it and/or modify it
#  under the terms of the GNU General Public License as published by the
#  Free Software Foundation; version 3.

#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA

# To the extent this program is licensed as part of the Enterprise
# versions of Cfengine, the applicable Commercial Open Source License
# (COSL) may apply to this file if you as a licensee so wish it. See
# included file COSL.txt.

###############################################################################
#+begin_src cfengine3
# for more info: http://cfengine.com/docs/master/reference-promise-types-edit_xml.html

body common control
{
      bundlesequence => { run };
      inputs => { "$(sys.libdir)/files.cf" };
}

bundle agent run
{
  vars:
      "file" string => "$(this.promise_filename).txt";
  methods:
      "rm" usebundle => rmxml;
      "make" usebundle => makexml;
      "use" usebundle => maintainxml;
      "report" usebundle => reportxml;
}

bundle agent rmxml
{
  files:
      "$(run.file)" delete => tidy;
}

bundle agent makexml
{
  files:
      "$(run.file)"
      comment => "Create xml file",
      create => "true",
      edit_defaults => empty,
      edit_xml => xml_insert_tree_nopath('<Host name="cfe_host"><Alias>cfe_alias</Alias></Host>');
}

bundle agent reportxml
{
  vars:
      "data" string => readfile($(run.file), 4k);
  reports:
      "Final XML is $(data)";
# R: Final XML is <?xml version="1.0"?>
# <Host name="newhostname">newhosttext<Alias>newalias</Alias></Host>

}

bundle agent maintainxml
{
  files:
      "$(run.file)"
      comment => "Maintain xml file: set the Alias node",
      create => "false",
      edit_xml => xml_set_value("newalias","/Host/Alias");

      "$(run.file)"
      comment => "Maintain xml file: replace a Host node with a name attribute = cfe_host",
      create => "false",
      edit_xml => xml_set_value("newhosttext","/Host[@name='cfe_host']");

      "$(run.file)"
      comment => "Maintain xml file: replace a Host node's name attribute",
      create => "false",
      edit_xml => xml_set_attribute("name", "newhostname", "/Host");
}
#+end_src
