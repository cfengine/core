#######################################################
#
# Test promise_kept and cancel_kept
# Insert lines to a file, then verify that insert promise is kept
#
# Present in both 00_basics/03_bodies and 10_files/05_classes
#
#######################################################

body common control
{
      inputs => { "../../default.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };
      version => "1.0";
}

#######################################################

bundle agent init
{
  methods:
      "" usebundle => dcs_all_negative_classes("t");
      "" usebundle => dcs_makefile($(G.testfile),
                                   $(dcs_strings.multiline_potatoes));
}

#######################################################

bundle agent test
{
  files:
      "$(G.testfile)"
      edit_line => insert_lines($(dcs_strings.multiline_potatoes)),
      edit_defaults => empty,
      classes => dcs_all_classes("t");
}

#######################################################

bundle agent check
{
  methods:
      "" usebundle => dcs_all_classes_to_string("t"),
      useresult => "actual";

      "" usebundle => dcs_check_strcmp('{ "t_cancel_notkept", "t_cancel_repaired", "t_promise_kept" }',
                                       "$(actual[str])",
                                       $(this.promise_filename),
                                       "no");
}
