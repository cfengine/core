
body common control
{
      bundlesequence => { "main" };
      inputs => { "namespace1.cf", "namespace2.cf", "namespace3.cf" };
}


bundle agent main
{
  vars: 

      "alien_list" slist => { getindices("name1:mymethod.array")};

      "local_list" slist => { getindices("example.array") };

  files:

      "/tmp/bla"

      create => "true",
      perms => zub("700");
      # or perms => default:zub("700");

  methods:

      "namespace demo" usebundle => name1:mymethod("arg1");

      "namespace demo" usebundle => name2:mymethod("arg1","arg2");

      "namespace demo" usebundle => example;
      # Or "namespace demo" usebundle => default:example;

      "namespace demo" usebundle => test_in_last_file;

  reports:

      "Foreign namespace index list: $(alien_list)";
      "Local index list: $(local_list)";
}


body perms zub(x)
{
      mode => "$(x)";
}


bundle agent example
{
  vars:

      "array[one]" string => "text one";
      "array[two]" string => "text two";
      "array[three]" string => "text three";

  reports:

      "TEST OK in no namespace";
}
