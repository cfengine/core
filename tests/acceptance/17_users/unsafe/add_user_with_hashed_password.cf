#######################################################
#
# Test adding a user with a password.
#
#######################################################

body common control
{
      inputs => { "../../default.cf.sub", "user_queries.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };
}

#######################################################

bundle agent init
{
  # Remove him first, should he already be present.
  users:
    "johndoe"
      policy => "absent";
}

#######################################################

bundle agent test
{
  vars:
    "hash" string => "dTloMVpjYt1w2";

  users:
    "johndoe"
      policy => "present",
      password => test_password;
}

body password test_password
{
  format => "hash";
  data => "$(test.hash)";
}

#######################################################

bundle agent check
{
  methods:
    "any" usebundle => user_has_password_hash("johndoe", "$(test.hash)", "success", "failure"),
      classes => always("methods_run");

  classes:
      "ready" expression => "methods_run";
      "ok" and => { "success", "!failure" };

  reports:
    # Password hash unsupported on Windows.
    (ok.ready)|windows::
      "$(this.promise_filename) Pass";
    (!ok.ready).!windows::
      "$(this.promise_filename) FAIL";
}
