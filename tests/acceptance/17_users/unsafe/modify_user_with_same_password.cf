#######################################################
#
# Test modifying a user with a password.
#
#######################################################

body common control
{
      inputs => { "../../default.cf.sub", "user_queries.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };
}

#######################################################

bundle agent init
{
  vars:
    # This is the same password as the plaintext one further down.
    "hash" string => "$1$vUsR5eoG$cci6C2jKvoYNuyjL5macS1";

  users:
    "johndoe"
      policy => "present",
      password => init_password;
}

body password init_password
{
  !windows::
    format => "hash";
    data => "$(init.hash)";
  windows::
    format => "plaintext";
    data => "J0hnd0eX";
}

#######################################################

bundle agent test
{
  users:
    "johndoe"
      policy => "present",
      password => test_password;
}

body password test_password
{
  format => "plaintext";
  data => "J0hnd0eX";
}

#######################################################

bundle agent check
{
  methods:
    # Make sure it is the same as before.
    !windows::
      "any" usebundle => user_has_password_hash("johndoe", "$(init.hash)", "success", "failure"),
        classes => default_cf_sub_set_class("methods_run");
    windows::
      "any" usebundle => user_has_password("johndoe", "J0hnd0eX", "success", "failure"),
        classes => default_cf_sub_set_class("methods_run");

  classes:
      "ready" expression => "methods_run";
      "ok" and => { "success", "!failure" };

  reports:
    ok.ready::
      "$(this.promise_filename) Pass";
    !ok.ready::
      "$(this.promise_filename) FAIL";
}
