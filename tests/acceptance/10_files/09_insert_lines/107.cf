#######################################################
#
# Insert lines from a variable, verify that pairs of whitespace_policy work
#
#######################################################

body common control
{
  inputs => { "../../default.cf" };
  bundlesequence  => { "g", default("$(this.promise_filename)") };   
  version => "1.0";
nova_edition::
  host_licenses_paid => "5";
}

bundle common g
{
vars:
	"testfile" string => "/tmp/TEST.cfengine";
}

#######################################################

bundle agent init
{
vars:
	"states" slist => { "actual", "expected" };

	"actual" string =>
"BEGIN
    One potato
    Two potato
    Two potatos
    Leading   embedded   and trailing spaces   
    Four
END";
 
      "insert" string =>
"    One potato
    Two potato
    Three potatoe
  Leading embedded and trailing spaces
    Four";

	"expected" string =>
"BEGIN
    One potato
    Two potato
    Two potatos
    Leading   embedded   and trailing spaces   
    Four
END
    Three potatoe
  Leading embedded and trailing spaces";
 
files:
	"$(g.testfile).$(states)"
	    create => "true",
	    edit_line => init_insert("$(init.$(states))"),
	    edit_defaults => init_empty;
}

bundle edit_line init_insert(str)
{
insert_lines:
	"$(str)";
}

body edit_defaults init_empty
{
	empty_file_before_editing => "true";
}

#######################################################

bundle agent test
{
files:
	"$(g.testfile).actual"
	    edit_line => test_insert("$(init.insert)");
}

bundle edit_line test_insert(lines)
{
insert_lines:
	"$(lines)"
	    whitespace_policy => { "ignore_trailing", "ignore_embedded" };
}

#######################################################

bundle agent check
{
methods:
	"any" usebundle => default_check_diff("$(g.testfile).actual",
					      "$(g.testfile).expected",
					      "$(this.promise_filename)");
}

#######################################################

bundle agent fini
{
methods:
	"any" usebundle => default_fini("$(g.testfile)");
}
