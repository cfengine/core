#######################################################
#
# Try to delete a region where select_end never matches
# Redmine #6589
#
#######################################################

body common control
{
      inputs => { "../../default.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };   
      version => "1.0";
}

bundle agent init
{
  vars:
      "states" slist => { "actual", "expected" };

      "actual" string =>
"header
BEGIN
    One potato
    Two potato
    Three potatoe
    Four
  END
trailer";
      "expected" string =>
"header
BEGIN
    One potato
    Two potato
    Three potatoe
    Four
  END
trailer";
 
  files:
      "$(G.testfile).$(states)"
      create => "true",
      edit_line => init_insert("$(init.$(states))"),
      edit_defaults => init_empty;
}

bundle edit_line init_insert(str)
{
  insert_lines:
      "$(str)";
}

body edit_defaults init_empty
{
      empty_file_before_editing => "true";
}

#######################################################

bundle agent test
{
  files:
      "$(G.testfile).actual"
      edit_line => test_delete(".*");
}

bundle edit_line test_delete(str)
{
  delete_lines:
      "$(str)"
      select_region => test_select;
}

body select_region test_select
{
      select_start => "BEGIN";
      select_end => "END"; # anchored regex doesn't match due to whitespace
}

#######################################################

bundle agent check
{
  methods:
      "any" usebundle => dcs_check_diff("$(G.testfile).actual",
					    "$(G.testfile).expected",
					    "$(this.promise_filename)");
}
### PROJECT_ID: core
### CATEGORY_ID: 27
