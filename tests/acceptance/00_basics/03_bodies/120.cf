#######################################################
#
# Test overlapping codes with command and *_returncodes
# Insert lines to a file, the grep for a line which is there
#
#######################################################

body common control
{
      inputs => { "../../default.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };
      version => "1.0";
}

#######################################################

bundle agent init
{
  methods:
      "" usebundle => dcs_all_negative_classes("t");
      "" usebundle => dcs_makefile($(G.testfile),
                                   $(dcs_strings.multiline_potatoes));
}

# These set/clear global classes, so they can be tested in another bundle
body classes test_all_classes(prefix)
{
      promise_kept => { "$(prefix)_promise_kept" };
      promise_repaired => { "$(prefix)_promise_repaired" };
      repair_failed => { "$(prefix)_repair_failed" };
      repair_denied => { "$(prefix)_repair_denied" };
      repair_timeout => { "$(prefix)_repair_timeout" };
      cancel_kept => { "$(prefix)_cancel_kept" };
      cancel_repaired => { "$(prefix)_cancel_repaired" };
      cancel_notkept => { "$(prefix)_cancel_notkept" };
      kept_returncodes => { "0", "1" };
      repaired_returncodes => { "1" };
      failed_returncodes => { "1", "2" };
}

#######################################################

bundle agent test
{
  commands:
      "$(G.grep) linenotfound $(G.testfile)"
      classes => test_all_classes("t");
}

#######################################################

bundle agent check
{
  methods:
      "" usebundle => dcs_all_classes_to_string("t"),
      useresult => "actual";

      "" usebundle => dcs_check_strcmp('{ "t_promise_kept", "t_promise_repaired", "t_repair_failed" }',
                                       "$(actual[str])",
                                       $(this.promise_filename),
                                       "no");
}
