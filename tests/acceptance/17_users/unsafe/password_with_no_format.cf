#######################################################
#
# Test missing password format
#
#######################################################

body common control
{
      inputs => { "../../default.cf.sub", "user_queries.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };
}

#######################################################

bundle agent init
{
  vars:
    # "j0hnd0e"
    "hash" string => "$1$vUsR5eoG$cci6C2jKvoYNuyjL5macS1";

  users:
    "johndoe"
      policy => "present",
      password => init_password;
}

body password init_password
{
  !windows::
    format => "hash";
    data => "$(init.hash)";
  windows::
    format => "plaintext";
    data => "Old0Password";
}

#######################################################

bundle agent test
{
  users:
    "johndoe"
      policy => "present",
      password => test_password;
}

body password test_password
{
  data => "NewPassword";
}

#######################################################

bundle agent check
{
  methods:
    # Make sure it is the same as before.
    !windows::
      "any" usebundle => user_has_password_hash("johndoe", "$(init.hash)", "success", "failure"),
        classes => default_cf_sub_set_class("methods_run");
    windows::
      "any" usebundle => user_has_password("johndoe", "Old0Password", "success", "failure"),
        classes => default_cf_sub_set_class("methods_run");

  classes:
      "ready" expression => "methods_run";
      "ok" and => { "success", "!failure" };

  reports:
    ok.ready::
      "$(this.promise_filename) Pass";
    !ok.ready::
      "$(this.promise_filename) FAIL";
}
