#######################################################
#
# Copy a file, then ensure that subsequent create=true doesn't
# overwrite mode, size, but DOES change owner, group (numeric IDs)
#
#######################################################

body common control
{
  inputs => { "../../default.cf.sub" };
  bundlesequence  => { default("$(this.promise_filename)") };
  version => "1.0";
}

bundle common g
{
vars:
        # This extracts the octal mode, and decimal nlink, uid, gid, size
        "command" string => 'printf "%05o" . " %d" x 4, (stat("$(G.testfile)"))[2]&07777, (stat(_))[3..5,7]';

        "source" string => "/etc/group";
        "source_command" string => 'printf "%d", (stat("$(g.source)"))[7]';
        "source_size" string => execresult(
            "/usr/bin/perl -le '$(g.source_command)'",
            "noshell");
}

#######################################################

bundle agent init
{
vars:
    any::
        "mode" int => "01751";
    linux::
        "owner" string => "sys";
        "group" string => "sys";
    freebsd::
        "owner" string => "bin";
        "group" string => "sys";
    !(linux|freebsd)::
        "owner" string => "undefined-please-fix";
        "group" string => "undefined-please-fix";

    pass2::
        "result" string => execresult(
            "/usr/bin/perl -le '$(g.command)'",
            "noshell");

files:
        "$(G.testfile)"
            copy_from => init_copy("$(g.source)"),
            perms => init_perms("$(mode)", "$(owner)", "$(group)"),
            classes => init_set_class("pass2");
}

body copy_from init_copy(file)
{
        source => "$(file)";
}

body perms init_perms(m, o, g)
{
mode => "$(m)";
owners => { "$(o)" };
groups => { "$(g)" };
}

body classes init_set_class(class)
{
promise_kept => { "$(class)" };
promise_repaired => { "$(class)" };
}

#######################################################

bundle agent test
{

files:
        "$(G.testfile)"
            create => "true",
            perms => test_perms("a+r");
}

body perms test_perms(m)
{
mode => "$(m)";
owners => { "333" };
groups => { "444" };
}


#######################################################

bundle agent check
{
vars:
        "result" string => execresult(
            "/usr/bin/perl -le '$(g.command)'",
            "noshell");
        "expected" string => "01755 1 333 444 $(g.source_size)";

classes:
        "ok" expression => strcmp("$(expected)", "$(result)");

reports:
    DEBUG::
        "init.result  is '$(init.result)'";
        "expected     is '$(expected)'";
        "check.result is '$(result)'";
    ok::
        "$(this.promise_filename) Pass";
    !ok::
        "$(this.promise_filename) FAIL";
}

