# Test that empty file is succesfully read by readfile() function

body common control
{
      inputs => { "../../default.cf.sub" };
      bundlesequence => { default("$(this.promise_filename)") };
      version => "1.0";
}

body location append
{
  before_after => "after";
}

bundle edit_line add_line(line)
{
  insert_lines:
       "$(line)" location => append;
}

bundle agent init
{
  vars:
      "indices" string => execresult("/usr/bin/seq 1 600", "noshell");
      "lines" slist => splitstring("$(indices)", "\n", "600");

  files:
      "$(G.testfile)"
        create => "true",
        edit_line => add_line("line$(lines)");
}

bundle agent test
{
  vars:
      "value" slist => readstringlist("$(G.testfile)", "", "\n", 100000, 1000000);
}

bundle agent check
{
  vars:
      "length" int => length("test.value");
      "last" string => nth("test.value", 599);
  classes:
      "ok1" expression => strcmp("$(length)", "600");
      "ok2" expression => strcmp("$(last)", "line600");
      "ok" and => { "ok1", "ok2" };

  reports:
    DEBUG.ok1::
      "passed1";
    DEBUG.ok2::
      "passed2";
    DEBUG.!ok1::
      "failed1 $(length) != 600";
    DEBUG.!ok2::
      "failed2 $(last) != line600";

    ok::
      "$(this.promise_filename) Pass";
    !ok::
      "$(this.promise_filename) FAIL";
}
