######################################################
#
# Test that or() behaves as expected
#
#####################################################
body common control
{
    inputs => { "../../default.cf.sub" };
    bundlesequence  => { default("$(this.promise_filename)") };
    version => "1.0";
}

#######################################################

bundle agent test
{
  meta:
    "description" -> { "CFE-3470" }
      string => "Test that or() behaves as expected";

  vars:
    "f" # false
      string => "(cfengine.(!cfengine))";
    "T" # true, uppercase to be more different visually
      string => "(cfengine|(!cfengine))";
    "f_name"
      string => "f";
    "T_name"
      string => "T";
    "f_name_name"
      string => "f_name";
    "T_name_name"
      string => "T_name";
    "many_true"
      slist => {
        "any",
        "$(T)",
        concat(not(or("$(f)"))),
        "(any.cfengine)",
        concat(not(or())),
        concat(not(or(or()))),
    };
    "many_false"
      slist => {
        "(!any)",
        "$(f)",
        concat(or(not("$(T)"))),
        "(any.!cfengine)",
        concat(not("any")),
        concat(or()),
      };
    "many_both"
      slist => { @(many_true), @(many_false) };

  classes:

    # All elements should be true, fail if one is false:
    "ok"
      scope => "namespace",
      and => {
        # Sanity check:
        "any",
        "cfengine",

        # or() with 0 arguments should default to false:
        not(or()),

        # or() with 1 static string:
        or("any"),
        or("cfengine"),
        or("!(!cfengine)"),

        # or() with 1 string with variable expansion(s):
        or("$(T)"),
        or("!$(f)"),
        or("$(T).any"),
        or("$(T).!(!any)"),
        or("any.$(T)"),
        or("!(!any).$(T)"),
        or("any|$(f)"),
        or("!(!any)|$(f)"),
        or("$(T)|$(f)"),
        or("$(f)|$(T)"),

        # or() with slist:
        # Careful, if there are expressions in list (using | operator)
        # they should be parenthesized for this to work:
        or(join(".", many_true)),
        or(join("|", many_true)),
        or(join("|", many_both)),
        or(not(join(".", many_false))),
        or(not(join("|", many_false))),
        or(not(join(".", many_both))),

        # or() with 1 function call as argument:
        or(or("any")),
        or(or("cfengine")),
        or(not("!cfengine")),
        or(not(not("cfengine"))),
        or(not("$(f)")),
        or(not(not("$(T)"))),
        or(strcmp("cfengine", "cfengine")),
        or(strcmp("any", not("$(f)"))),

        # or() with 2 arguments:
        or("any", "cfengine"),
        or("any", "!any"),
        or("!any", "any"),
        or("!(!any)", "!(!cfengine)"),
        or("$(T)", "$(T)"),
        or("$(T)", "$(f)"),
        or("$(T)", "!$(f)"),
        or("$(T)", not("$(f)")),
        or(not("$(f)"), not("$(f)")),

        # or() with 3+ arguments (strings):
        or("any", "any", "any"),
        or("any", "any", "any", "any"),
        or("any", "any", "any", "any", "any"),
        or("any", "any", "any", "any", "any", "any"),
        or("any", "any", "any", "any", "any", "!any"),
        or("any", "any", "any", "any", "!any", "!any"),
        or("any", "any", "any", "!any", "!any", "!any"),
        or("any", "any", "!any", "!any", "!any", "!any"),
        or("any", "!any", "!any", "!any", "!any", "!any"),
        or("!any", "!any", "!any", "!any", "!any", "any"),
        or("!any", "!any", "!any", "!any", "any", "any"),
        or("!any", "!any", "!any", "any", "any", "any"),
        or("!any", "!any", "any", "any", "any", "any"),
        or("!any", "any", "any", "any", "any", "any"),
        or("$(T)", "$(T)", "$(T)"),
        or("$(T)", "$(T)", "$(T)", "$(T)"),
        or("$(T)", "$(T)", "$(T)", "$(T)", "$(T)"),
        or("$(T)", "$(T)", "$(T)", "$(T)", "$(T)", "$(T)"),
        or("$(T)", "$(T)", "$(T)", "$(T)", "$(T)", "$(f)"),
        or("$(T)", "$(T)", "$(T)", "$(T)", "$(f)", "$(f)"),
        or("$(T)", "$(T)", "$(T)", "$(f)", "$(f)", "$(f)"),
        or("$(T)", "$(T)", "$(f)", "$(f)", "$(f)", "$(f)"),
        or("$(T)", "$(f)", "$(f)", "$(f)", "$(f)", "$(f)"),
        or("$(f)", "$(f)", "$(f)", "$(f)", "$(f)", "$(T)"),
        or("$(f)", "$(f)", "$(f)", "$(f)", "$(T)", "$(T)"),
        or("$(f)", "$(f)", "$(f)", "$(T)", "$(T)", "$(T)"),
        or("$(f)", "$(f)", "$(T)", "$(T)", "$(T)", "$(T)"),
        or("$(f)", "$(T)", "$(T)", "$(T)", "$(T)", "$(T)"),

        # or() with 3+ function calls:
        or(not("any"), not("any"), not("!any")),
        or(not("any"), not("any"), not("any"), not("!any")),
        or(not("any"), not("any"), not("any"), not("any"), not("!any")),
        or(not("any"), not("any"), not("any"), not("any"), not("any"), not("!any")),
        or(not("$(T)"), not("$(T)"), not("$(f)")),
        or(not("$(T)"), not("$(T)"), not("$(T)"), not("$(f)")),
        or(not("$(T)"), not("$(T)"), not("$(T)"), not("$(T)"), not("$(f)")),
        or(not("$(T)"), not("$(T)"), not("$(T)"), not("$(T)"), not("$(T)"), not("$(f)")),

        # or() with deep nesting:
        not(or(or())),
        not(or(or(or()))),
        not(or(or(or(or())))),
        not(or(or(or(or(or()))))),
        not(or(or(or(or(or(or())))))),
        or(or(or(or(or(or("any")))))),
        or(or(or(or(or(or("any", "cfengine")))))),

        # Double expansion:
        or("$($(T_name))"),
        or("$($(f_name))", "$($(T_name))"),
        or("$($(f_name))", "$($(f_name))", "$($(T_name))"),
        or("!$($(T_name))", "!$($(f_name))"),
        or("!$($(T_name))", "!$($(T_name))", "!$($(f_name))"),
        or(not("$($(T_name))"), not("$($(f_name))")),

        # Triple expansion:
        or("$($($(T_name_name)))"),
        or("$($($(f_name_name)))", "$($($(T_name_name)))"),
        or("$($($(f_name_name)))", "$($($(f_name_name)))", "$($($(T_name_name)))"),
        or("!$($(T_name_name))", "!$($(f_name_name))"),
        or("!$($(T_name_name))", "!$($(T_name_name))", "!$($(f_name_name))"),
        or(not("$($(T_name_name))"), not("$($(f_name_name))")),

        # or() should always return any or !any,
        # this is important for backwards compatibility:
        strcmp(or("any"), "any"),
        strcmp(or("!any"), "!any"),
        strcmp(or("!cfengine"), "!any"),
        strcmp(or("!(cfengine|!cfengine)"), "!any"),
        strcmp(or("$(T)"), "any"),
        strcmp(or("$(f)"), "!any"),
        strcmp(or("$(T)", "$(T)"), "any"),
        strcmp(or("$(T)", "$(f)"), "any"),
        strcmp(or("$(f)", "$(T)"), "any"),
        strcmp(or("$(f)", "$(f)"), "!any"),
      };

    # Cases where or() should return false (fail if one is true):
    "fail_false"
      or => {
        or("$(f)"),
        or("$(f)", "$(f)"),
        or("$(f)", "$(f)", "$(f)"),
        or("$(f)", "$(f)", "$(f)", "$(f)"),
        or("$(f)", "$(f)", "$(f)", "$(f)", "$(f)"),
        or("$(f)", "$(f)", "$(f)", "$(f)", "$(f)", "$(f)"),
        or("$(f)", "$(f)", "$(f)", "$(f)", "$(f)", "$(f)", "$(f)"),
        or("$(f)", "$(f)", "$(f)", "$(f)", "$(f)", "$(f)", "$(f)", "$(f)"),
      };
    # Should be skipped because of unresolved variable:
    "fail_unresolved"
      and => {
        "any",
        or("$(unresolved_var)"),
      };
    # Check that it's really skipped because of unresolved,
    # and not that it accidentally becomes false:
    "fail_not_of_unresolved"
      and => {
        "any",
        or(not("$(unresolved_var)")),
      };
    "fail"
      scope => "namespace",
      expression => "fail_false|fail_unresolved|fail_not_of_unresolved";
}

#######################################################

bundle agent check
{

  reports:
    ok.(!fail)::
      "$(this.promise_filename) Pass";
    (!ok)|fail::
      "$(this.promise_filename) FAIL";
}
