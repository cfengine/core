#######################################################
#
# Test maparray()
#
#######################################################

body common control
{
      inputs => { "../../default.cf.sub" };
      bundlesequence  => { default("$(this.promise_filename)") };
      version => "1.0";
}

#######################################################

bundle agent init
{
  vars:
      "dummy" string => "dummy";

  files:
      "$(G.testfile).expected"
      create => "true",
      edit_line => init_insert;
}

bundle edit_line init_insert
{
  insert_lines:
      "key=alpha value=zero";
      "key=beta value=two";
      "key=gamma's value=three's";
      "key=delta-delta:delta value=four-fore:quatre";
      "key=last value=last";
}

#######################################################

bundle agent test
{
  vars:
      "array[alpha]" string => "zero";
      "array[beta]" string => "two";
      "array[gamma's]" string => "three's";
      "array[delta-delta:delta]" string => "four-fore:quatre";
      "array[last]" string => "last";

      "mapped" slist => maparray("key=$(this.k) value=$(this.v)", "array");

  files:
      "$(G.testfile).actual"
      create => "true",
      edit_line => test_insert;

  reports:
    DEBUG::
      "Inserting line: $(mapped)";
}

bundle edit_line test_insert
{
  vars:
      "mapped" slist => { @{test.mapped} };

  insert_lines:
      "$(mapped)";
}

#######################################################

bundle agent check
{
  methods:
      "any" usebundle => sorted_check_diff("$(G.testfile).actual",
                                           "$(G.testfile).expected",
                                           "$(this.promise_filename)");
}

